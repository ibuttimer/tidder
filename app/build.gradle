apply plugin: 'com.android.application'

/* The keystore.properties file in the project root should contain the following properties for the
    key to use sign the application for release:
    storePassword=myStorePassword
    keyPassword=mykeyPassword
    keyAlias=myKeyAlias
    storeFile=myStoreFileLocation
 */
def keystorePropertiesFile = rootProject.file("keystore.properties")
if( !keystorePropertiesFile.exists() ) {
    keystorePropertiesFile.text = """
        storePassword=myStorePassword
        keyPassword=mykeyPassword
        keyAlias=myKeyAlias
        storeFile=myStoreFileLocation"""
}
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.ianbuttimer.tidderish"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode 4
        versionName "1.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        resValue "string", "widget_authority_provider", "com.ianbuttimer.tidderish.widget"
        buildConfigField "String", "PROVIDER_AUTHORITY", "\"com.ianbuttimer.tidderish.data.db.gen\""
        resValue "string", "authority_provider", "com.ianbuttimer.tidderish.data.db.gen"
        buildConfigField "String", "FB_PROVIDER_AUTHORITY", "\"com.ianbuttimer.tidderish.data.db\""
        resValue "string", "fb_authority_provider", "com.ianbuttimer.tidderish.data.db"

        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        // see https://developer.android.com/studio/publish/app-signing#sign-auto
        archive {
            // an example configuration to allow app signing during the build process
            storePassword "Keys2thekingdom"
            keyAlias "tidderkey"
            keyPassword "reddit"
            storeFile file("keystore/keystore.jks")
        }
        propfile {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "tier"

    productFlavors {
        // see https://github.com/ibuttimer/tidder#features for details of features
        free {
            // ad-supported, local db, no post pin functionality
            dimension "tier"
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
            signingConfig signingConfigs.archive
        }
        full {
            // no ads, firebase realtime db, post pin functionality
            dimension "tier"
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
            signingConfig signingConfigs.archive
        }
        freeMax {
            // no ads, local db, post pin functionality
            dimension "tier"
            applicationIdSuffix ".freeMax"
            versionNameSuffix "-freeMax"
            signingConfig signingConfigs.propfile
        }
    }
    sourceSets {
        free {
            java.srcDirs = ['src/main/java', 'src/free/java', 'src/localdb/java']
        }
        freeMax {
            java.srcDirs = ['src/main/java', 'src/freeMax/java', 'src/localdb/java']
        }
    }
    // Butterknife requires Java 8.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    def xLegacyVersion = "1.0.0"
    def supportLibVersion = "27.1.1"
    def glideVersion = '4.12.0'
    def parcelerVersion = "1.1.13"
    def schematicVersion = "0.7.0"
    def stethoVersion = "1.5.1"
    def firebaseVersion = '19.6.0'
    def recyclerViewVersion = "1.2.1"

    def preference_version = "1.1.1"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "androidx.preference:preference:$preference_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation "androidx.palette:palette:$xLegacyVersion"
    implementation "androidx.cardview:cardview:$xLegacyVersion"
    implementation "androidx.viewpager:viewpager:$xLegacyVersion"
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // define a BOM and its version
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.0"))
    implementation("com.squareup.okhttp3:okhttp")
//    implementation("com.squareup.okhttp3:logging-interceptor")
    implementation("com.squareup.okio:okio:2.10.0")

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "org.parceler:parceler-api:$parcelerVersion"
    annotationProcessor "org.parceler:parceler:$parcelerVersion"
    implementation 'org.greenrobot:eventbus:3.2.0'

    /* The current artifact on bintray.com is empty so for the moment, use a local version compiled using the
        source from https://github.com/nisrulz/recyclerviewhelper
    implementation "com.github.nisrulz:recyclerviewhelper:x$recyclerViewVersion"
     */
    implementation(name:'recyclerviewhelper', ext:'aar')

    implementation "net.opacapp:multiline-collapsingtoolbar:$supportLibVersion"
    implementation 'com.google.guava:guava:30.0-android'
    implementation 'com.github.tiagohm.MarkdownView:library:0.19.0'
    implementation 'com.google.code.gson:gson:2.8.6'
//    implementation "com.google.firebase:firebase-core:$firebaseVersion"

    debugImplementation "com.facebook.stetho:stetho:$stethoVersion"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    // TODO migrate to guava version of eventbus or make local versions of guava functions used (may eliminate need for multidex)
    fullImplementation "com.google.firebase:firebase-database:$firebaseVersion"

    freeAnnotationProcessor "net.simonvt.schematic:schematic-compiler:$schematicVersion"
    freeImplementation "net.simonvt.schematic:schematic:$schematicVersion"
    freeImplementation 'com.google.android.gms:play-services-ads:20.4.0'

    freeMaxAnnotationProcessor "net.simonvt.schematic:schematic-compiler:$schematicVersion"
    freeMaxImplementation "net.simonvt.schematic:schematic:$schematicVersion"
}

apply plugin: 'com.google.gms.google-services'
