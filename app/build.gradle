apply plugin: 'com.android.application'

/* The keystore.properties file in the project root should contain the following properties:
    storePassword=myStorePassword
    keyPassword=mykeyPassword
    keyAlias=myKeyAlias
    storeFile=myStoreFileLocation
 */
def keystorePropertiesFile = rootProject.file("keystore.properties")
if( !keystorePropertiesFile.exists() ) {
    keystorePropertiesFile.text = """
        storePassword=myStorePassword
        keyPassword=mykeyPassword
        keyAlias=myKeyAlias
        storeFile=myStoreFileLocation"""
}
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.ianbuttimer.tidder"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        resValue "string", "widget_authority_provider", "com.ianbuttimer.tidder.widget"
        buildConfigField "String", "PROVIDER_AUTHORITY", "\"com.ianbuttimer.tidder.data.db.gen\""
        resValue "string", "authority_provider", "com.ianbuttimer.tidder.data.db.gen"
        buildConfigField "String", "FB_PROVIDER_AUTHORITY", "\"com.ianbuttimer.tidder.data.db\""
        resValue "string", "fb_authority_provider", "com.ianbuttimer.tidder.data.db"

        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        archive {
            storeFile file("keystore/keystore.jks")
            storePassword "Keys2thekingdom"
            keyAlias "tidderkey"
            keyPassword "reddit"
        }
        propfile {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "tier"

    productFlavors {
        free {
            // ad-supported, local db, no post pin functionality
            dimension "tier"
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
            signingConfig signingConfigs.archive
        }
        full {
            // no ads, firebase realtime db, post pin functionality
            dimension "tier"
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
            signingConfig signingConfigs.archive
        }
        freeMax {
            // no ads, local db, post pin functionality
            dimension "tier"
            applicationIdSuffix ".freeMax"
            versionNameSuffix "-freeMax"
            signingConfig signingConfigs.propfile
        }
    }
    sourceSets {
        free {
            java.srcDirs = ['src/main/java', 'src/free/java', 'src/localdb/java']
        }
        freeMax {
            java.srcDirs = ['src/main/java', 'src/freeMax/java', 'src/localdb/java']
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:customtabs:$rootProject.ext.supportLibVersion"  // to resolve mixed support lib versions in dependencies
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:palette-v7:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:preference-v7:$rootProject.ext.supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation "com.android.support:support-v4:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:support-vector-drawable:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:support-v4:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:design:$rootProject.ext.supportLibVersion"
    implementation "com.android.support:cardview-v7:$rootProject.ext.supportLibVersion"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "com.facebook.stetho:stetho:$rootProject.ext.stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$rootProject.ext.stethoVersion"
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.jakewharton.timber:timber:4.7.0'
    implementation "com.jakewharton:butterknife:$rootProject.ext.butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterknifeVersion"
    implementation "com.github.bumptech.glide:glide:$rootProject.ext.glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$rootProject.ext.glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.ext.glideVersion"
    implementation "org.parceler:parceler-api:$rootProject.ext.parcelerVersion"
    annotationProcessor "org.parceler:parceler:$rootProject.ext.parcelerVersion"
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation "com.github.nisrulz:recyclerviewhelper:$rootProject.ext.supportLibVersion"
    implementation "net.opacapp:multiline-collapsingtoolbar:$rootProject.ext.supportLibVersion"
    implementation 'com.google.guava:guava:25.1-android'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.github.tiagohm.MarkdownView:library:0.19.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "com.google.firebase:firebase-core:$rootProject.ext.firebaseVersion"

    // TODO migrate to guava version of eventbus or make local versions of guava functions used (may eliminate need for multidex)
    fullImplementation "com.google.firebase:firebase-database:$rootProject.ext.firebaseVersion"

    freeAnnotationProcessor "net.simonvt.schematic:schematic-compiler:$rootProject.ext.schematicVersion"
    freeImplementation "net.simonvt.schematic:schematic:$rootProject.ext.schematicVersion"
    freeImplementation 'com.google.android.gms:play-services-ads:15.0.1'

    freeMaxAnnotationProcessor "net.simonvt.schematic:schematic-compiler:$rootProject.ext.schematicVersion"
    freeMaxImplementation "net.simonvt.schematic:schematic:$rootProject.ext.schematicVersion"
}

apply plugin: 'com.google.gms.google-services'
